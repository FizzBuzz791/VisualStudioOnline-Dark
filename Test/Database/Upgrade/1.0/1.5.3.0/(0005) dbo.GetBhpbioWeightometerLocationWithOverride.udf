IF Object_Id('dbo.GetBhpbioWeightometerLocationWithOverride') IS NOT NULL 
     DROP FUNCTION dbo.GetBhpbioWeightometerLocationWithOverride
GO

CREATE FUNCTION dbo.GetBhpbioWeightometerLocationWithOverride
(
	@iDateFrom DATETIME,
	@iDateTo DATETIME
)
RETURNS @WeightometerLocation TABLE
(
	Weightometer_Id VARCHAR(31) NOT NULL,
	Location_Id INT NOT NULL,
	Location_Type_Id INT NOT NULL,
	IncludeStart DATETIME NOT NULL,
	IncludeEnd DATETIME NOT NULL,	
	
	Primary KEY (Weightometer_Id, Location_Id, IncludeStart, IncludeEnd)
)
BEGIN
	Declare @GlobalStartDate DateTime
	Declare @GlobalEndDate DateTime
	Declare @CreateDate DateTime
		
	SET @GlobalStartDate = '1900-01-01'
	SET @GlobalEndDate = '2050-12-31'  
	SET @CreateDate = GETDATE()	
	
	DECLARE @WeightometerLocationWorking TABLE
	(
		Weightometer_Id VARCHAR(31) NOT NULL,
		Period_Order INT NOT NULL,
		Location_Id INT NOT NULL,
		Location_Type_Id INT NOT NULL,
		IncludeStart DATETIME NOT NULL,
		IncludeEnd DATETIME NOT NULL,	
	
		Primary KEY (Weightometer_Id, Period_Order, IncludeStart)	
	)
	
	-- Combine WeightometerLocation and BhpbioWeightometerLocationOverride tables
	INSERT INTO @WeightometerLocationWorking
	(
		Weightometer_Id, Period_Order, Location_Id, Location_Type_Id, IncludeStart, IncludeEnd
	)

	SELECT	Weightometer_Id, 0 AS Period_Order, Location_Id, Location_Type_Id, 
			@GlobalStartDate AS IncludeStart, @GlobalEndDate AS End_Date
	FROM	WeightometerLocation
	UNION ALL
	SELECT DISTINCT	Weightometer_Id, ROW_NUMBER() OVER (PARTITION BY Weightometer_Id ORDER BY FromMonth) AS Period_Order,
			Location_Id, Location_Type_Id, FromMonth AS IncludeStart, ToMonth AS IncludeEnd
	FROM	BhpbioWeightometerLocationOverride

	-- correct overlapping date ranges (force end date prior to start date of next period)
	UPDATE	CLW1 SET IncludeEnd = DATEADD(DAY, -1, CLW2.IncludeStart)
	FROM	@WeightometerLocationWorking CLW1
	INNER JOIN @WeightometerLocationWorking CLW2 
		ON	CLW2.Weightometer_Id = CLW1.Weightometer_Id
		AND	CLW2.Period_Order = CLW1.Period_Order + 1
	  AND CLW2.IncludeStart < CLW1.IncludeEnd

	-- fill any gaps the above update may have created - subsequent to overridden periods
	INSERT INTO @WeightometerLocationWorking
	(
	  Weightometer_Id, Location_Id, Location_Type_Id, IncludeEnd, Period_Order, IncludeStart
	)
	SELECT CLW.Weightometer_Id, CL.Location_Id, CL.Location_Type_Id, @GlobalEndDate, CLW.Period_Order + 1, DATEADD(DAY, 1, CLW.IncludeEnd)
	FROM @WeightometerLocationWorking CLW
	INNER JOIN WeightometerLocation CL ON CL.Weightometer_Id = CLW.Weightometer_Id 
	INNER JOIN (
	  SELECT CLW3.Weightometer_Id, MAX(CLW3.Period_Order) As Final_Period_Order, MAX(CLW3.IncludeEnd) As Final_IncludeEnd
	  FROM @WeightometerLocationWorking CLW3
	  GROUP BY CLW3.Weightometer_Id
	  HAVING MAX(CLW3.IncludeEnd) < @GlobalEndDate
	) FCL ON FCL.Weightometer_Id = CLW.Weightometer_Id AND CLW.IncludeEnd = FCL.Final_IncludeEnd

	
	-- fill any gaps the above update may have created - between overridden periods
	INSERT INTO @WeightometerLocationWorking
	(
	  Weightometer_Id, Location_Id, Location_Type_Id, IncludeEnd, Period_Order, IncludeStart
	)
	SELECT CLW.Weightometer_Id, CL.Location_Id, CL.Location_Type_Id, DATEADD(DAY, -1, CLW2.IncludeStart), CLW.Period_Order, DATEADD(DAY, 1, CLW.IncludeEnd)
	FROM @WeightometerLocationWorking CLW
	INNER JOIN @WeightometerLocationWorking CLW2 ON CLW.Weightometer_Id = CLW2.Weightometer_Id AND CLW.Period_Order + 1 = CLW2.Period_Order
	INNER JOIN WeightometerLocation CL ON CLW.Weightometer_Id = CL.Weightometer_Id 
	WHERE CLW.Period_Order > 0
	AND DATEADD(DAY, 1, CLW.IncludeEnd) < CLW2.IncludeStart
	
	INSERT INTO @WeightometerLocation 
	(
	  Weightometer_Id, Location_Id, Location_Type_Id, IncludeEnd, IncludeStart
	)	
  SELECT Weightometer_Id, Location_Id, Location_Type_Id, 
    CASE WHEN IncludeEnd > @iDateTo THEN @iDateTo ELSE IncludeEnd END, 
    CASE WHEN IncludeStart < @iDateFrom THEN @iDateFrom ELSE IncludeStart END
  FROM @WeightometerLocationWorking
  WHERE IncludeStart < @iDateTo
  AND IncludeEnd > @iDateFrom

	RETURN
END
GO